#compdef vimpck

local context
local state line ret=1
local plugins
plugins=($(vimpck ls 2>/dev/null))

_arguments -C \
  '1: :->cmds' \
  '*:: :->args' && ret=0

case $state in
  cmds)
    _values "main subcommand" \
        "install[install plugins defined in the vimpckrc]" \
        "upgrade[upgrade plugins]" \
        "ls[list plugins]" \
        "rm[remove plugins]"
    ret=0
    ;;
  args)
    case $line[1] in
      upgrade)
        [[ -n "$plugins" ]] && _values 'plugins' $plugins
        ret=0
        ;;
      ls)
        _values "ls command" \
            "--start[list only autostarting plugins]" \
            "--opt[list only optional plugins]"
        ret=0
        ;;
      rm)
        [[ -n "$plugins" ]] && _values 'plugins' $plugins
        ret=0
        ;;
    esac
    ;;
esac

return ret

# TODO: Find how to display multiple _values categories (for upgrade and rm
# command)
